//模板编译
//dotNet.desktop 扩展库使用了这种技术
import win.version;
import dotNet; 
var compiler = dotNet.createCompiler("C#");

compiler.Source = /*** 
?> //C#源码如果起始于本行前面的 aardio 模板标记则支持模板语法,参考:《aardio 使用手册 / aardio 语言 / 模板语法》
namespace CSharpLibrary  
{  
    public class Object  
    {     
    	<? if _WINXP { ?> 
        public string Test(){    
            return "Windows XP"; 
        }
        <? } else { ?> 
        public string Test(){    
            return "<?= win.version.name ?>"; 
        }
        <? } ?> 
    }   
} 
***/

//编译内存程序集，导入名字空间 
compiler.import("CSharpLibrary");  

//使用 C# 编写的类构造对象实例 
var netObj = CSharpLibrary.Object();

//调用实时编译的C#函数
var ret = netObj.Test(); 

import console;
console.log( ret );
console.pause();