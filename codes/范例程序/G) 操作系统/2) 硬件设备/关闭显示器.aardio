//自动关屏
import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text="自动关屏";left=-20;top=50;right=319;bottom=99;bgcolor=16777215;border="none";max=false)
winform.add(
btnClose={cls="close";text="×";left=297;top=11;right=325;bottom=39;bgcolor=13405076;dr=1;dt=1;font=LOGFONT(h=-21);z=3};
btnCloseLcd={cls="plus";text="自动关屏";left=176;top=14;right=240;bottom=33;color=8388608;notify=1;textPadding={left=5};z=5};
chkLock={cls="plus";text="锁屏";left=242;top=14;right=296;bottom=33;align="left";iconStyle={align="left";font=LOGFONT(name='FontAwesome')};iconText='\uF0C8 ';notify=1;textPadding={left=15};z=4};
datetimepick={cls="datetimepick";left=111;top=14;right=172;bottom=38;edge=1;transparent=1;updown=1;z=1};
static={cls="static";text="空闲时间超过：";left=0;top=18;right=103;bottom=40;align="right";transparent=1;z=2}
)
/*}}*/

import fsys.config;
var config = fsys.config(io.appData("aardio/std/closeLcd"))
if(!config.setting.time) config.setting.time = time.iso8601(20)

winform.datetimepick.setFormat("' 'mm':'ss");
winform.datetimepick.time = config.setting.time

winform.datetimepick.onDateTimeChanged = function(dateTime,none){
 
	if(tonumber(dateTime)<5){
		winform.msgboxErr("设定的时间不能小于 5 秒！");
		winform.datetimepick.time = time.iso8601(5);
		return;
	}
	
	config.setting.time =  winform.datetimepick.time;
	config.setting.save();  
}

import sys;
winform.setInterval( 
	function(){
		if(win.getIdleTime() > (tonumber(config.setting.time)*1000) ){
			 ::User32.PostMessage( 0xFFFF/*_HWND_BROADCAST*/ ,0x112/*_WM_SYSCOMMAND*/, 0xF170/*_SC_MONITORPOWER*/ ,2);
			 if(config.setting.lock){
			 	sys.lock();
			 }
		}
	},1000 
)

winform.onMouseDown = function(){
	winform.hitCaption()
}

winform.chkLock.skin(
    color = { 
        hover = 0xFFFF0000; 
        active = 0xFF00FF00;
    }
    checked = { 
    	iconText = '\uF14a';
    } 
)  
winform.chkLock.oncommand = function(id,event){
	config.setting.lock = winform.chkLock.checked;
	config.setting.save();
}

winform.btnCloseLcd.oncommand = function(id,event){
    //延时异步执行，避免显示器刚关掉又被唤醒
    winform.setTimeout( 
        function(){ 
            //用:User32.SendMessage会卡，改用异步的::User32.PostMessage
            ::User32.PostMessage( 0xFFFF/*_HWND_BROADCAST*/ ,0x112/*_WM_SYSCOMMAND*/, 0xF170/*_SC_MONITORPOWER*/ ,2);
            
            if(config.setting.lock){
			 	sys.lock();
			}
        },200 //给一点延时，万一鼠标还在晃呢?
    )	
}

winform.chkLock.checked = config.setting.lock;

winform.btnCloseLcd.skin({
	color={
		active=0xFF00FF00;
		default=0xFF000080;
		disabled=0xFF6D6D6D;
		hover=0xFFFF0000
	}
})

//点击 close 控件关闭窗口。
winform.btnClose.oncommand = function(id,event){
	winform.close();
}
winform.btnClose.show(true);

import win.ui.shadow;
win.ui.shadow(winform);

winform.show(); 
win.loopMessage();