//v8 开发包
import java;
import zlib.httpFile;
import fsys;
import web.rest.client; 

namespace java.jdk.v8;
path = null;

var jdkDir = ..io.appData("aardio/std/OpenJDK/jdk8");
if(..io.exist(jdkDir)){
	..fsys.enum( jdkDir, "jdk*",
		function(dirname,filename,fullpath,findData){ 
			if(!filename){ 
				path = fullpath; 
				return false;
			}
		} 
		,false
	);	
}
 
if(!..io.exist(path)){
	var http = ..web.rest.client();
	var openJdkMirrors = http.api("https://mirrors.tuna.tsinghua.edu.cn/AdoptOpenJDK/8/jdk/x32/windows/","GET",`href\="(OpenJDK8U-jdk_x86-32_windows_hotspot_\w+)\.zip"`);
	var zipName = openJdkMirrors.get() 
 
	var dir = ..zlib.httpFile.download("https://mirrors.tuna.tsinghua.edu.cn/AdoptOpenJDK/8/jdk/x32/windows/" + zipName +".zip"
		,"正在下载本程序必须组件: OpenJDK v8",..io.appData("aardio/std/OpenJDK"),jdkDir,"jdk8.zip")	
 
	if(!dir) return; 
	if(..io.exist(jdkDir)){
		..fsys.enum( jdkDir, "jdk*",
			function(dirname,filename,fullpath,findData){ 
				if(!filename){ 
					path = fullpath; 
					return false;
				}
			} 
			,false
		);	
	}
	
 	if(!path) return;
}
 
..java.path.setJdkHome(path)

/**intellisense()
java.jdk.v8 = 检测并在必要时自动下载 OpenJDK v8,\n导入此库后，发布不用带 JDK,\n导入此库已自动导入 java 库
java.jdk.v8.path = 如果成功下载并设为 JDK 根目录,\n此属性为 JDK 根目录路径,\n否则此属性值为 null
end intellisense**/