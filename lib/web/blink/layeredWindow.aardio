//layeredWindow 分层透明窗口
if( _STUDIO_INVOKED && _STUDIO_INVOKED != "process" ){ 
	error("web.blink不能在IDE嵌入线程模式下启动!",2)
}

import web.blink;
namespace web.blink;
var wke = _dll;

class layeredWindow{
	ctor( winform ){
		if( ! (winform[["hwnd"]] &&  winform.isForm) ) error("参数@1必须是win.form创建的窗体对象",2);
		
		if( winform.className == "static" ){
			winform.modifyStyle(,0x100/*_SS_NOTIFY*/ | 0x2000000/*_WS_CLIPCHILDREN*/); 
		} 
		else{
			if( winform.isForm &&  (! ..table.next( winform._ctrlHwnd) ) 
				&& ( ! ..win.getStyle(winform.hwnd,0x40000000/*_WS_CHILD*/) ) ){
				::CreateWindowEx(0x20/*_WS_EX_TRANSPARENT*/,"static",""
					,0x44000000/*_WS_CHILD | _WS_CLIPSIBLINGS*/ ,-2,-2,1,1,winform.hwnd,winform._getFreeCommandId(), _HINSTANSE ); 
			}
			winform.modifyStyle(,0x2000000/*_WS_CLIPCHILDREN*/);
			
			if(winform.isForm){
				winform.modifyStyle(0xC40000/*_WS_BORDER | _WS_CAPTION | _WS_SIZEBOX | _WS_DLGFRAME*/)
			}
		};
		
		if( winform.isDialogMessage ){
			if( winform.isDialogMessage!= ..win.isDialogMessage ){
				error("在创建网页窗体以前,禁止重写默认 isDialogMessage() 函数 ",2)
			}
			else{
				winform.isDialogMessage = function(hwnd,msg) {
					return false; 
				}
			}
		};
		
		this = ..web.blink.view();
		this.transparent = true;
		
		wke.wkeSetHandle(this.hWebView,winform.hwnd);
		this._form = winform;
		winform.transparent(true);
		
		if( winform.dpiScaleX && winform.dpiScaleX !== 1 ){
			this.zoomFactor = winform.dpiScaleX;
		}
		winform.afterDpiChanged = function(scaleX){
			this.zoomFactor = scaleX;	
		}
		
		var rect = winform.getClientRect();
		this.resize(rect.width(),rect.height());
		var sizeWnd = ::SIZE(rect.width(),rect.height());
		var blendFunction = ..gdi.blendFunction();
		
		var needRedrawWindow = 0;
		var isIconic = ..win.isIconic;
		winform.wndproc = function(hwnd,message,wParam,lParam){
			if( message == 0x2/*_WM_DESTROY*/){
				this.destroy();
				return;
			}
			
			if( this.hWebView && (message!=0x113/*_WM_TIMER*/) ){
				var handled,retCode;
				handled,retCode,needRedrawWindow = WebViewWndProc(this.hWebView,hwnd,message,wParam,lParam,0,needRedrawWindow);
				if( message == 0x5/*_WM_SIZE*/){
					sizeWnd = ::SIZE( ::LOWORD(lParam), ::HIWORD(lParam) );
					return;
				}
				
				if( needRedrawWindow ){
					if( !isIconic(hwnd) ) ..gdi.updateLayeredWindow( winform.hwnd,this.getDc(),sizeWnd,blendFunction ); 
				}
				
				if(handled) return retCode;
			}
		}
		
		this.onPaintUpdated = function(hdc,x,y,cx,cy){
			..gdi.updateLayeredWindow( winform.hwnd,hdc,sizeWnd,blendFunction ); 
		}
	};
	getForm = function(top){
		if( top === null ) top = true;
		return  ..win.form.getForm( this._form.hwnd );
		if( !top && ..win.isChild(form.hwnd,hwnd) ){
			return form.getCtrl( hwnd );
		}
		return form;
	}; 
	getCaret = function(){
		if(owner.hWebView) {
			var r = {int x;int y;int width;int height};
			wkex.wkexGetCaret(owner.hWebView,r); 
			if(r.height) return r;
		}
	};
} 

namespace layeredWindow{
    wkex = ..web.blink._dllx;
	WebViewWndProc = wkex.api("WebViewWndProc","bool(addr hView,addr hwnd,INT message,ADDR wParam,addr lParam,int &retCode,int &needRedrawWindow)" ); 
}

/**intellisense()
web.blink.layeredWindow = 创建分层透明窗口用于显示透明网页
web.blink.layeredWindow(.(winform) = 参数为窗口对象,创建分层透明窗口用于显示透明网页\n注意:任何网页返回的js对象应当现用现取,不可保存在aardio中\n延时或任何窗口消息分发都可能导致js传给aardio的对象被回收
web.blink.layeredWindow() = !web_blink_view.
end intellisense**/

