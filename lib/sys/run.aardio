//run 启动项
import sys.reg;
namespace sys;

class run {
	ctor(taskName){
		this.taskName = taskName || ..io.splitpath(..io._exepath).name;
	};
	delete = function(){
		if(_STUDIO_INVOKED) return;
		..sys.reg.setValue(this.taskName,null,"Software\Microsoft\Windows\CurrentVersion\Run\"); 
	};
	register = function(arguments,path){
		if(_STUDIO_INVOKED) return;
		
		var path = `"`+ ..io.fullpath(path : ..io._exepath) + `"`; 
		if(type(arguments)=="string") {
			path = path +" "+ arguments; 
		}
		
		..sys.reg.setValue(this.taskName,path,"Software\Microsoft\Windows\CurrentVersion\Run\"); 
	};
} 

/**intellisense()
sys.run = 用于为当前用户注册开机启动项，以普通权限启动，\n如果希望以管理权限启动且不出现警告窗口，请改用 sys.runAsTask\n\n建议大家不要滥用此功能，\n用户未在设置中主动设置开机启动，请不要自动添加，\n仅为当前用户添加开机启动，任何时候都不建议设置所有用户开机启动项。\n滥用自动开机只会招致用户反感，或被安全软件清除
sys.run(.(taskName) = 创建注册与删除开机启动项工具\n可选用 @taskName 参数指定启动项名称，不指定则为当前文件名
sys.run() = !stdSysRun.
!stdSysRun.delete() = 删除开机启动项\n在开发环境中此函数不执行操作，仅发布后会生效
!stdSysRun.register( = 添加开机启动项\n在开发环境中此函数不执行操作，仅发布后会生效
!stdSysRun.register(.(arguments,path,workDir) = 添加开机启动项\n可选用 @arguments 参数指定启动参数，此参数必须是字符串。\n可选用 @path 参数指定要启动的应用程序路径,省略则为当前应用程序路径
end intellisense**/