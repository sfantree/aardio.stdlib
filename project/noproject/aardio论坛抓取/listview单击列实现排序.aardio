
// listview 单击列实现排序
import win.ui;
/*DSG{{*/
var winform = win.form(text="aardio Form";right=549;bottom=426;parent=...)
winform.add(
listview={cls="listview";left=18;top=16;right=532;bottom=407;bgcolor=16777215;edge=1;font=LOGFONT(name='宋体');fullRow=1;gridLines=1;z=1}
)
/*}}*/

// 初始化 imagelist
import win.imageList;
var iml = win.imageList(16, 15);
iml.add('GIF\56\57a \0\15\0\x80\0\0\x80\x80\x80\xff\0\xff\33\xf9\4\0\0\0\0\0\44\0\0\0\0 \0\15\0\0\2\31\x8c\x8f\xa9\xcb\xed\15\xa3\x9c\xb4N\xf0\x80\xde\56k\xbfA\\\xd7\x84 \x97Y\xea\xca\xb6\xee\11\xc7F\1\0;', 0xff00ff);
winform.listview.setColumnImageList(iml);

/*
   初始化信息
   注：此处的人名及地址均为虚构
*/
var info = {
    {
        id = 1;
        name = "Ben";
        addr = "720, Beijing, China";
    };
    {
        id = 2;
        name = "Angela";
        addr = "120, Shanghai, China";
    };
    {
        id = 3;
        name = "Mike";
        addr = "220, New York, USA";
    }
}
// 初始化表头
winform.listview.insertColumn("编号", 70, 1);
winform.listview.insertColumn("姓名", 150, 2);
winform.listview.insertColumn("地址", 250, 3);

// 填入信息
var k, v;
for (k, v in info) {
    var col = winform.listview.addItem( {
        tostring( v.id );
        v.name;
        v.addr
    } );
}

// 排序函数
winform.listview.sortBy = function (ind) {
    var image = winform.listview.getColumnImage(ind);
    // 已按照升序排序，则按照降序排序；其余情况按照升序排序
    var desc = image == 0;
    // 冒泡排序，大家也可采用其他排序方式
    var i, j, k;
    var num = winform.listview.count;
    var strCurrect, strLast, strTemp;
    for (j = 2; num) {
        for (i = num; j; -1) {
            strCurrect = winform.listview.getItemText(i, ind);
            strLast = winform.listview.getItemText(i - 1, ind);
            if (strCurrect < strLast && !desc || strCurrect > strLast && desc) {
                // 交换两行的位置
                for (k = 1; 3) {
                    strTemp = winform.listview.getItemText(i, k);
                    winform.listview.setItemText(winform.listview.getItemText(i - 1, k), i, k);
                    winform.listview.setItemText(strTemp, i - 1, k);
                }
            }
        }
    }
    // 更新列图标
    winform.listview.clearColumnImage()
    winform.listview.setColumnImage(ind, desc ? 1 : 0);
}

winform.listview.onnotify = function(id,code,ptr){
    // 处理列被单击事件
    if (code == 0xFFFFFF94/*_LVN_COLUMNCLICK*/) {
        var nmlvw = winform.listview.getNotifyMessage(code,ptr)
        winform.listview.sortBy( nmlvw.iSubItem );
    }
}

winform.show();
win.loopMessage();